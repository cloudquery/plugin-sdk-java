plugins {
    id 'java-library'
    id "io.freefair.lombok" version "8.6"
    id "maven-publish"
    id "com.diffplug.spotless" version "6.25.0"
}

ext {
   javaMainClass = "io.cloudquery.MainClass"
}

group 'io.cloudquery'
// x-release-please-start-version
version = '0.0.25'
// x-release-please-end

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/cloudquery/plugin-pb-java")
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    mavenCentral()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'org.jooq:joou:0.9.4'
    implementation 'com.google.guava:guava:33.1.0-jre'
    implementation 'info.picocli:picocli:4.7.5'
    implementation 'com.google.guava:guava:33.1.0-jre'
    implementation 'io.grpc:grpc-protobuf:1.62.2'
    implementation 'io.grpc:grpc-stub:1.62.2'
    implementation 'io.grpc:grpc-services:1.62.2'
    implementation 'io.cloudquery:plugin-pb-java:0.0.15'
    implementation 'org.apache.arrow:arrow-memory-core:15.0.2'
    implementation 'org.apache.arrow:arrow-vector:15.0.2'
    implementation 'commons-io:commons-io:2.16.0'

    implementation "com.fasterxml.jackson.core:jackson-core:2.17.0"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.17.0"

    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'

    testImplementation 'io.grpc:grpc-testing:1.62.2'
    testImplementation 'io.grpc:grpc-inprocess:1.62.2'
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.16'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

    runtimeOnly "org.apache.arrow:arrow-memory-netty:15.0.2"
}

test {
    useJUnitPlatform()
    testLogging {
        events  "skipped", "failed"
    }
    jvmArgs("--add-opens=java.base/java.nio=ALL-UNNAMED")
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "io.cloudquery.MainClass"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cloudquery/plugin-sdk-java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId = 'plugin-sdk-java'
            from(components.java)
        }
    }
}

task runMemDBServe(type: JavaExec) {
    group = "Execution"
    description = "Start the MemDB plugin server"
    classpath = sourceSets.main.runtimeClasspath
    main = javaMainClass
    args = ["serve"]
    jvmArgs = ["--add-opens=java.base/java.nio=ALL-UNNAMED"]
}

spotless {
  java {
    googleJavaFormat()
  }
}
