plugins {
    id 'java-library'
    id "io.freefair.lombok" version "8.10.2"
    id "maven-publish"
    id "com.diffplug.spotless" version "6.25.0"
}

ext {
   javaMainClass = "io.cloudquery.MainClass"
}

group 'io.cloudquery'
// x-release-please-start-version
version = '0.0.35'
// x-release-please-end

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/cloudquery/plugin-pb-java")
        credentials {
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
    mavenCentral()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'org.jooq:joou:0.9.4'
    implementation 'com.google.guava:guava:33.3.1-jre'
    implementation 'info.picocli:picocli:4.7.6'
    implementation 'com.google.guava:guava:33.3.1-jre'
    implementation 'io.grpc:grpc-protobuf:1.68.1'
    implementation 'io.grpc:grpc-stub:1.68.1'
    implementation 'io.grpc:grpc-services:1.68.1'
    implementation 'io.cloudquery:plugin-pb-java:0.0.27'
    implementation 'org.apache.arrow:arrow-memory-core:17.0.0'
    implementation 'org.apache.arrow:arrow-vector:17.0.0'
    implementation 'commons-io:commons-io:2.18.0'

    implementation "com.fasterxml.jackson.core:jackson-core:2.18.1"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.18.1"

    implementation 'org.apache.logging.log4j:log4j-api:2.24.2'
    implementation 'org.apache.logging.log4j:log4j-core:2.24.2'

    testImplementation 'io.grpc:grpc-testing:1.68.1'
    testImplementation 'io.grpc:grpc-inprocess:1.68.1'
    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
    testImplementation 'org.mockito:mockito-core:5.13.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.13.0'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.17.3'
    testImplementation 'org.assertj:assertj-core:3.26.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'

    runtimeOnly "org.apache.arrow:arrow-memory-netty:17.0.0"
}

test {
    useJUnitPlatform()
    testLogging {
        events  "skipped", "failed"
    }
    jvmArgs("--add-opens=java.base/java.nio=ALL-UNNAMED")
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes "Main-Class": "io.cloudquery.MainClass"
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cloudquery/plugin-sdk-java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifactId = 'plugin-sdk-java'
            from(components.java)
        }
    }
}

task runMemDBServe(type: JavaExec) {
    group = "Execution"
    description = "Start the MemDB plugin server"
    classpath = sourceSets.main.runtimeClasspath
    main = javaMainClass
    args = ["serve"]
    jvmArgs = ["--add-opens=java.base/java.nio=ALL-UNNAMED"]
}

spotless {
  java {
    googleJavaFormat()
  }
}
